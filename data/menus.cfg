// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

exec "data/maps.cfg"

togglemainmenu = [
    if (! (cleargui 1)) [
        showgui main
    ]
]

bind ESCAPE [togglemainmenu]

checkrolloveraction = [
    ? (>= (strstr $guirolloveraction $arg1) 0) $guirolloveraction
]

newgui main [

	// clear new token var again, just to make 120% sure
	newtoken = ""

	guitext (concat (gettrans "gui.main.name") (getname)) (playermodelicon)
    guibutton (concat (gettrans "gui.main.player") (playermodelname)) [guirolloveraction = (playermodelbutton $playermodel); showgui playermodel] (playermodelicon)
    if $offline [
		guibutton (concatword "^f3" (gettrans "gui.network.offline")) [offline 0] "radio_off"
	] [
		guibutton (concatword "^f0" (gettrans "gui.network.online")) [offline 1] "radio_on"
	]
	guibar
    // guibutton "t:gui.main.multiplayer" "showgui servers"
    if (isconnected) [
        if (|| $editing (m_edit (getmode))) [
            guibutton "t:gui.main.editing" "showgui editing"
        ]
        guibutton "t:gui.main.voting" "showgui gamemode"
        guibutton "t:gui.main.switchteam" [if (strcmp (getteam) "blue") [team red] [team blue]]
        guibutton "t:gui.main.togglespec" [spectator (! (isspectator (getclientnum)))] "spectator"
        guibutton "t:gui.main.moptions" [showgui master]
        guibar
        guibutton "t:gui.main.options"  "showgui options"
        guibutton "t:gui.main.disconnect" "disconnect" "exit"
    ] [
        //guibutton "t:gui.main.alphaserver" "showgui alphaservers"
		//guibutton "t:gui.main.alphaserver" "connect hnss.gq"
		guibutton "t:gui.main.multiplayer" "showgui servers"
        guibutton "t:gui.main.botmatch" "showgui botmatch"
        guibar
        guibutton "t:gui.main.options" "showgui options"
        guibutton "t:gui.main.about" "showgui about"
        guibutton "t:gui.main.quit" "quit" "exit"
    ]
] 0

newgui alphaservers [
    guitext "Select a region:"
    guibutton "North America (NA)" "connect hnss.gq"
    guibutton "Europe (EU)" "connect eu.hnss.gq"
	guibutton "Fallback (NA)" "connect gay.hnss.gq"
] "Alpha Servers"

newgui about [
    guitext "Carmine Impact"
    guitext [by Zsolt "Yellowberry" Zitting, Jacob "Bounty.jpeg" Peterson]
    guitext ["Detimilu", Joseph "NoobGuy" Smythe, Johnathan "C0baltCooled", and many others.]
    guitext "(for a full list of contributors see the readme or check the map!)"
	guitext "^f5In loving memory of Marvin Christensen Zitting."
    //guibutton "Open credits map" "mode 17; map special/credits"
    guitext "^f3https://harpnetstudios.com/games/carmine"

    guitab "Cube 2"
    guitext "Cube 2: Sauerbraten"
    guitext [by Wouter "Aardappel" van Oortmerssen, Lee "eihrul" Salzman,]
    guitext [Mike "Gilt" Dysart, Robert "baby-rabbit" Pointon,]
    guitext [John "geartrooper" Siar, Quinton "Quin" Reeves, and others]
    guitext "(for a full list of contributors see the readme)"
    guitext "^f3http://sauerbraten.org/"
] "Carmine Impact"

//17
macro playermodelbutton [cleargui 1; playermodel %1]

newgui playermodel [
    guilist [
        guilist [
            loop i $playermodelnum [
                guibutton (playermodelname $i) (playermodelbutton $i) (playermodelicon $i)
            ]
        ]
        guibar
        pmidx = (substr $guirolloveraction (strlen (playermodelbutton "")))
        pmidx = (max 0 (min (- $playermodelnum 1) $pmidx))
        guilist [
            guiplayerpreview $pmidx 0 2 (checkrolloveraction "playermodel ") 4 1
            guitextbox (playermodelstory $pmidx) 40 9
        ]
    ]
    guitab "t:gui.player.setup"

    guilist [
        guiradio "t:gui.player.fp" thirdperson 0 "thirdperson 0"
        guibar
        guiradio "t:gui.player.tp" thirdperson 1 "thirdperson 1"
    ]
    if $thirdperson [
        guitext "t:gui.player.tp.dist"
        guislider thirdpersondistance 10 30
        guitext "t:gui.player.tp.up"
        guislider thirdpersonup -10 10
        guitext "t:gui.player.tp.side"
        guislider thirdpersonside -10 10
    ]
	guibar
	guilist [
		guibutton "t:gui.player.color.rifle" "showgui rifletrail"
		guibar
		guibutton "t:gui.player.color.blood" "showgui bloodcolor"
	]

] "t:gui.player.pm"

pmteam = 0
pmgun = 6
pmscale = 4
pmol = 1

newgui playermodeltesting [
    guilist [
        guilist [
            loop i $playermodelnum [
                guibutton (playermodelname $i) (playermodelbutton $i) (playermodelicon $i)
            ]
        ]
        guibar
        pmidx = (substr $guirolloveraction (strlen (playermodelbutton "")))
        pmidx = (max 0 (min (- $playermodelnum 1) $pmidx))
        guilist [
            guiplayerpreview $pmidx $pmteam $pmgun $guirolloveraction $pmscale $pmol
            guitextbox (playermodelstory $pmidx) 40 9
            guitext "Team: "
            guislider pmteam 0 2
            guitext "Weapon: "
            guislider pmgun 0 7
            guitext "Scale: "
            guislider pmscale 0 4
            guicheckbox "Overlaid:" pmol
        ]
    ]
] "pm"

// crosshairs = ["data/crosshair.png data/hit.png"
//"packages/crosshairs/x.png packages/crosshairs/x-hit.png"
//"packages/crosshairs/x_dot.png packages/crosshairs/x_dot-hit.png"
//"packages/crosshairs/o.png packages/crosshairs/o-hit.png"
//"packages/crosshairs/o_x.png packages/crosshairs/o_x-hit.png"
//"packages/crosshairs/o_dot.png packages/crosshairs/o_dot-hit.png"
//"packages/crosshairs/dot.png packages/crosshairs/dot-hit.png"
//"packages/crosshairs/dot_wide.png packages/crosshairs/dot_wide-hit.png"
//"packages/crosshairs/star.png packages/crosshairs/star-hit.png"
//"packages/crosshairs/wide.png packages/crosshairs/wide-hit.png"
//"packages/crosshairs/circle_dot.png packages/crosshairs/circle_dot_hit.png"
//"packages/crosshairs/cross_normal.png packages/crosshairs/cross_normal_hit.png"
//"packages/crosshairs/cross_whole.png packages/crosshairs/cross_whole_hit.png"
//"packages/crosshairs/dot_normal.png packages/crosshairs/dot_normal_hit.png"
//"packages/crosshairs/dot_whole.png packages/crosshairs/dot_whole_hit.png"
//]

crosshairs = ["data/crosshair.png data/hit.png"]
    loopfiles curch "packages/crosshairs" "png" [
        crosshairs = (concat $crosshairs (concatword "packages/crosshairs/" (concatword $curch ".png")))
    ]
crosshairs = (sortlist $crosshairs x y [<=s $x $y])

newgui crosshair [
    guitext "crosshair size"
    guislider crosshairsize
    loop row (div (+ (listlen $crosshairs) 6) 7) [
        guilist [
            loop col 7 [
                ch = (at $crosshairs (+ (* $row 7) $col))
                if (!=s $ch "") [
                    guiimage (at $ch 0) [
                        cleargui 1
                        loadcrosshair @(at $ch 0) 0
                        loadcrosshair @(at $ch (if (> (listlen $ch) 1) 1 0)) 2
                    ] 0.5
                ]
            ]
        ]
    ]
]

newgui master [
  guistayopen [
    if (ismaster (getclientnum)) [
        guibutton "t:gui.master.unclaim" "setmaster 0"
    ] [
        guibutton "t:gui.master.claim" "setmaster 1"
    ]
    guilist [
        guitext "t:gui.master.mode" 
        mm = (getmastermode)
        guiradio "t:gui.master.mode.open" mm 0 "mastermode 0"
        guiradio "t:gui.master.mode.veto" mm 1 "mastermode 1"
        guiradio "t:gui.master.mode.lock" mm 2 "mastermode 2"
        guiradio "t:gui.master.mode.priv" mm 3 "mastermode 3"
    ]
    guibar
    looplist cn (listclients 0 0) [
        guilist [
            guitext (concatword (getclientname $cn) ": ") (getclienticon $cn)
            guibutton "t:gui.master.cn.kick" [kick @cn]
            guibutton "t:gui.master.cn.spec" [spectator @(= (isspectator $cn) 0) @cn]
            if (m_teammode (getmode)) [
                guibutton "t:gui.master.cn.team" [setteam @cn @(? (=s (getclientteam $cn) "blue") "red" "blue")]
            ]
            if (ismaster $cn) [
                guibutton "t:gui.master.cn.takem" [setmaster 0 @cn]
            ] [
                guibutton "t:gui.master.cn.givem" [setmaster 1 @cn]
            ]
        ]
    ]
  ]
] "t:gui.master"

botmatchcount = 5
botmatchminskill = 50
botmatchmaxskill = 100

startbotmatch = [
    if (isconnected 0 0) [
        echo (gettrans "gui.botmatch.disconnect")
        disconnect
    ]
    loop i $botmatchcount [addbot (rnd (+ $botmatchmaxskill 1) $botmatchminskill)]
]

newgui botmatch [
    guitext "t:gui.botmatch.amount"
    guislider botmatchcount 0 32
    guitext "t:gui.botmatch.min"
    guislider botmatchminskill 50 101 [if (< $botmatchmaxskill $botmatchminskill) [botmatchmaxskill = $botmatchminskill]]
    guitext "t:gui.botmatch.max"
    guislider botmatchmaxskill 50 101 [if (> $botmatchminskill $botmatchmaxskill) [botmatchminskill = $botmatchmaxskill]]
    guibutton "t:gui.botmatch.start" [guionclear [startbotmatch]; showgui gamemode]
] "t:gui.botmatch"

newgui servers [
    guistayopen [
        guiservers [
            guilist [
                guibutton "t:gui.servers.update" "updatefrommaster"
                guibar
                guicheckbox "t:gui.servers.lan" searchlan
                guibar
                guicheckbox "t:gui.servers.autoupdate" autoupdateservers
                guibar
                guicheckbox "t:gui.servers.autosort" autosortservers
                if (= $autosortservers 0) [
                    guibar
                    guibutton "t:gui.servers.sort" "sortservers"
                ]
            ]
            guibar
        ] 17
    ]
] "t:gui.servers" [initservers]

newgui gamemode [
    guilist [
        guilist [
            guibutton "FFA (WIP)"                   "mode 0; showgui maps"
            guibutton "Co-Op Edit (WIP)"            "mode 1; showgui maps"
            guibutton "Team Deathmatch (WIP)"       "mode 2; showgui maps"
            guibutton "Instagib"                    "mode 3; showgui maps"
            guibutton "Team Instagib"               "mode 4; showgui maps"
            guibutton "Tactics"                     "mode 5; showgui maps"
            guibutton "Team Tactics"                "mode 6; showgui maps"
            guibutton "Capture (WIP)"               "mode 7; showgui maps"
            guibutton "Regen Capture (WIP)"         "mode 8; showgui maps"
            guibutton "CTF (WIP)"                   "mode 9; showgui maps"
            guibutton "Insta-CTF (WIP)"             "mode 10; showgui maps"
            guibutton "Protect (WIP)"               "mode 11; showgui maps"
            guibutton "Insta-Protect (WIP)"         "mode 12; showgui maps"
            guibutton "Hold (WIP)"                  "mode 13; showgui maps"
            guibutton "Insta-Hold (WIP)"            "mode 14; showgui maps"
            ]
        guibar
        guilist [
            guibutton "Collect (WIP)"               "mode 15; showgui maps"
            guibutton "Insta-Collect (WIP)"         "mode 16; showgui maps"
            guibutton "Parkour"                     "mode 17; showgui maps"
            guibutton "Insta-Tactics"               "mode 18; showgui maps"
            guibutton "Team Instagib Tactics"       "mode 19; showgui maps"
            guibutton "Grenade Battle"              "mode 20; showgui maps"
            guibutton "Gun Game (WIP)"              "mode 21; showgui maps"
            guibutton "Last Man Standing (WIP)"     "mode 22; showgui maps"
        ]
    ]
] "t:gui.gamemode"

showfileeditor = [
    guinoautotab [
        guieditor $arg1 $arg2 $arg3
        textinit $arg1 $arg1
        guistayopen [
            guilist [
                guibutton "t:gui.fileedit.load" [textfocus @arg1; textload @arg1]
                guibar
                guibutton "t:gui.fileedit.save" [textfocus @arg1; textsave @arg1]
                guibar
                guibutton "t:gui.fileedit.exec" [textfocus @arg1; textexec]
                guibar
                guibutton "t:gui.fileedit.copy" [textfocus @arg1; textcopy]
                guibar
                guibutton "t:gui.fileedit.paste" [textfocus @arg1; textpaste]
                guibar
                guibutton "t:gui.fileedit.select" [textfocus @arg1; textselectall]
                guibar
                guibutton "t:gui.fileedit.clear" [textfocus @arg1; textclear]
            ]
        ]
    ]
]

notepadfile = "untitled.txt"

newgui notepad [
    guifield notepadfile -30
    showfileeditor $notepadfile -80 20
] "t:gui.notepad"

notepad = [
    if (> $numargs 0) [notepadfile = $arg1]
    showgui notepad
]

newgui pastebuffer [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guilist [
                guibutton "t:gui.fileedit.exec" [textfocus "#pastebuffer"; textexec]
                guibar
                guibutton "t:gui.fileedit.clear" [textfocus "#pastebuffer"; textclear]
            ]
        ]
    ]
] "t:gui.pbuffer"

pastebuffer = [showgui pastebuffer]

newgui scratchpad [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guilist [
                guibutton "t:gui.fileedit.exec" [textfocus "#scratchpad"; textexec]
                guibar
                guibutton "t:gui.fileedit.copy" [textfocus "#scratchpad"; textcopy]
                guibar
                guibutton "t:gui.fileedit.paste" [textfocus "#scratchpad"; textpaste]
                guibar
                guibutton "t:gui.fileedit.select" [textfocus "#scratchpad"; textselectall]
                guibar
                guibutton "t:gui.fileedit.clear" [textfocus "#scratchpad"; textclear]
            ]
        ]
    ]
] "t:gui.spad"

scratchpad = [showgui scratchpad]

newmapsize = 12
savemap_name = temp

showmmgui = [showgui mapmodels]

newgui editing [
    guibutton "t:gui.edit.materials" "showgui materials"
    guibutton "t:gui.edit.toggle" "edittoggle"
    guibutton "t:gui.edit.deselect" "cancelsel"
    guibutton "t:gui.edit.passthru" "passthrough"
    guibutton "t:gui.edit.copy" "copy"
    guibutton "t:gui.edit.paste" "paste"

    guitab "t:gui.edit.ent"
    guibutton "t:gui.edit.ent.drop" "showgui dropent"
    guibutton "t:gui.edit.ent.mm" "showgui mapmodels"
    guibutton "t:gui.edit.ent.light" "resetlight; showgui newlight"
    guibutton "t:gui.edit.ent.tele" "showgui newteleport"
    guibutton "t:gui.edit.ent.trig" "showgui triggers"

    guitab "t:gui.edit.key"
    guibutton "t:gui.edit.key.flip" "flip"
    guibutton "t:gui.edit.key.undo" "undo"
    guibutton "t:gui.edit.key.redo" "redo"
    guibutton "t:gui.edit.key.delete" "editdel"
    guibutton "t:gui.edit.key.texgui" "showtexgui"
    guibutton "t:gui.edit.key.entgui" "showentgui"
    guibutton "t:gui.edit.key.mmgui" "showmmgui"
    guibutton "t:gui.edit.key.entinsel" "entselect insel"
    guibutton "t:gui.edit.key.entfindall" "selentfindall"
    guibutton "t:gui.edit.key.entview" "entautoview"
    guibutton "t:gui.edit.key.entcon" "selentedit"
    guitext "t:gui.edit.key.hmtex"
    guicheckbox "t:gui.edit.key.outline" outline
    guicheckbox "t:gui.edit.key.wireframe" wireframe

    guitab "t:gui.edit.scroll"
    guitext "t:gui.edit.scroll.sel"
    guitext "t:gui.edit.scroll.face"
    guitext "t:gui.edit.scroll.grid"
    guitext "t:gui.edit.scroll.tex"
    guitext "t:gui.edit.scroll.rot"
    guitext "t:gui.edit.scroll.corner"
    guitext "t:gui.edit.scroll.hm"
    guitext "t:gui.edit.scroll.hmbrush"

    guitab "t:gui.edit.light"
    guibutton "t:gui.edit.light.calcslow" "calclight 1"
    guibutton "t:gui.edit.light.calcquick" "calclight -1"
    guibutton "t:gui.edit.light.patchlight" "patchlight"

    guicheckbox "t:gui.edit.light.fullbright" fullbright

    guitext "t:gui.edit.light.lightprecision"
    guilistslider lightprecision "8 16 32 48 64 128 256"

    guitext "t:gui.edit.light.lighterror"
    guislider lighterror

    guitext "t:gui.edit.light.lightthreads"
    guislider lightthreads

    //guibutton "lightlod high (6)" "lightlod 6"
    //guibutton "lightlod low  (2)" "lightlod 2"
    //guibutton "lightlod off  (0)" "lightlod 0"


    guitab "t:gui.edit.blend"
    guitext "t:gui.edit.blend.mode"
	guiradio "t:gui.edit.blend.mode.off" blendpaintmode 0
	guiradio "t:gui.edit.blend.mode.replace" blendpaintmode 1
	guiradio "t:gui.edit.blend.mode.dig" blendpaintmode 2
	guiradio "t:gui.edit.blend.mode.fill" blendpaintmode 3
	guiradio "t:gui.edit.blend.mode.invdig" blendpaintmode 4
	guiradio "t:gui.edit.blend.mode.invfill" blendpaintmode 5
    guibar
    guitext "t:gui.edit.blend.map" 
	guibutton "t:gui.edit.blend.map.change" "nextblendbrush"
	guibutton "t:gui.edit.blend.map.rotate" "rotateblendbrush"
	guibutton "t:gui.edit.blend.map.clear" "clearblendmap"
	guibutton "t:gui.edit.blend.map.clearsel" "clearblendmapsel"

    guitab "t:gui.edit.map" 
    guitext   "t:gui.edit.map.savemap"
    guifield  savemap_name 20 [ savemap $savemap_name ]
    guibutton "t:gui.edit.map.reloadmap" "map $savemap_name"
    guibutton "t:gui.edit.map.newmap" "newmap $newmapsize"
    guislider "newmapsize" 10 16
    guibutton "t:gui.edit.map.mapenlarge" "mapenlarge"
    guitext   "t:gui.edit.map.maxundo"
    guislider "undomegs" 1 10
    guibutton "t:gui.edit.map.optimize" "remip"
    guibutton "t:gui.edit.map.maptitle" "saycommand [/maptitle ]"
    guibutton "t:gui.edit.map.skybox" "showgui skybox"

    guitab "t:gui.edit.config"
    guitext (mapcfgname)
    showfileeditor (mapcfgname) -70 15
] "t:gui.edit"

newgui skybox [
   guilist [
        guistayopen [
            guilist [ genskyitems $skies1 ]
            guilist [ genskyitems $skies2 ]
            showskyshot
        ]
    ]

    guitab 2
    guilist [
         guistayopen [
            guilist [ genskyitems $skies3 ]
            guilist [ genskyitems $skies4 ]
            showskyshot
        ]
    ]
	guitab 3
    guilist [
         guistayopen [
            guilist [ genskyitems $skies5 ]
            guilist [ genskyitems $skies6 ]
            showskyshot
        ]
    ]

    guitab "t:gui.edit.config"
    guitext (mapcfgname)
    showfileeditor (mapcfgname) -60 15
] "t:gui.skybox"

skies1 = [
mayhem/afterrain
mayhem/aqua4
mayhem/aqua9
mayhem/coulee
mayhem/duomo
mayhem/eclipse
mayhem/grouse
mayhem/flame
mayhem/forestrytwo
mayhem/h2s
mayhem/prehistoric
mayhem/scorched
mayhem/shattered
mayhem/trough
penguins/arid
penguins/arid2
penguins/barren
] //529
skies2 = [
penguins/battery
penguins/bay
penguins/blizzard
penguins/cocoa
penguins/desertdawn
penguins/divine
penguins/dusk
penguins/dust
penguins/flames
penguins/gloom
penguins/harmony
penguins/haze
penguins/heather
penguins/humble
penguins/kenon_star
penguins/majik
penguins/meadow
] //546
skies3 = [
penguins/mellow
penguins/morning
penguins/mystic
penguins/ocean
penguins/overcast
penguins/paze
penguins/pit
penguins/quirk
penguins/raspberry
penguins/serenity
penguins/sleepyhollow
penguins/sun
penguins/tears
penguins/torture
penguins/trance
penguins/tropic
penguins/trouble
]
skies4 = [
penguins/valley
penguins/violence
penguins/vulcan
penguins/wasteland
penguins/wrath
penguins/yonder
penguins/zeus
skyboxes/black
skyboxes/clearsky052
skyboxes/darkness
skyboxes/evilsky
skyboxes/morning
skyboxes/remus/sky01
skyboxes/white
staffy/staffy
ulukai/redeclipse
ulukai/corona
]
skies5 = [
red/clouds
red/clouds2
red/gradient
red/grey
red/interstellar
red/miramar
red/skyhigh
red/skylump
red/skylump2
red/stars
red/stormydays
red/sunsetflat
red/yellow
loopix/daysky
skorpio/weltraum
kurt/space
skyboxes/test/test
]
skies6 = [
bkg/blue/bkg1
bkg/lightblue/bkg1
bkg/red/bkg1
bkg/red/bkg2
bkg/red/bkg3
]

genskyitems = [
    looplist cursky $arg1 [
        guibutton $cursky (concat skybox $cursky)
    ]
]

alias showskyshot [ guibar; guiimage (concatword "<thumbnail:128>packages/" (if (> $numargs 0) [result $arg1] [result (at $guirollovername 0)]) "_ft.png") (checkrolloveraction "skybox ") 4 1 "data/cardboard_missing.png"]


newgui materials [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "gameclip"                   "editmat gameclip"
    guibutton "death"                      "editmat death"
    guibutton "alpha"                      "editmat alpha"
	guibutton "spaceclip"                  "editmat spaceclip"

    guicheckbox "t:gui.materials.showmat"    showmat
] "t:gui.materials"

setting_entediting = 1

newgui dropent [
    guiradio "t:gui.dropent.floor" entdrop 1
    guiradio "t:gui.dropent.sel" entdrop 2
    guiradio "t:gui.dropent.floorsel" entdrop 3
    guiradio "t:gui.dropent.pos" entdrop 0
    guibar
    guicheckbox "t:gui.dropent.selsnap" entselsnap
    guicheckbox "t:gui.dropent.entedit" setting_entediting 1 0 [ entediting $setting_entediting ]
] "t:gui.dropent"

newgui triggers [
    guibutton [Invisible]               [BTmm = -1; showgui trigger2]
    guibutton [Switch #1]               [BTmm = 24; showgui trigger2]
    guibutton [Switch #2]               [BTmm = 25; showgui trigger2]
    guibutton [Door #1]                 [BTmm = 26; showgui trigger2]
    guibutton [Door #2]                 [BTmm = 27; showgui trigger2]
    guibutton [Door #3]                 [BTmm = 28; showgui trigger2]
]

newgui trigger2 [
    guibutton [Animation loop]                      [BTtt = 0; showgui trigger3]
    guibutton [Trigger once (popback)]              [BTtt = 1; showgui trigger3]
    guibutton [Trigger once (popback & rumble)]     [BTtt = 2; showgui trigger3]
    guibutton [Trigger once]                        [BTtt = 3; showgui trigger3]
    guibutton [Trigger once (rumble)]               [BTtt = 4; showgui trigger3]
    guibutton [Trigger multiple (popback)]          [BTtt = 5; showgui trigger3]
    guibutton [Trigger multiple (popback & rumble)] [BTtt = 6; showgui trigger3]
    guibutton [Trigger multiple]                    [BTtt = 7; showgui trigger3]
    guibutton [Trigger multiple (rumble)]           [BTtt = 8; showgui trigger3]
    guibutton [Door (open once)]                    [BTtt = 9; showgui trigger3]
    guibutton [Door (auto close)]                   [BTtt = 10; showgui trigger3]
    guibutton [Door (locked)]                       [BTtt = 11; showgui trigger3]
    guibutton [Vanishing trigger]                   [BTtt = 12; showgui trigger3]
    guibutton [Vanishing trigger (rumble)]          [BTtt = 13; showgui trigger3]
    guibutton [End of level trigger]                [BTtt = 29; showgui trigger3]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]

newgui trigger3 [
    loop i 9 [
        tjt = (concat tag (concatword # $i))
        tjc = (concat BTti "=" $i ";" "build_trigger")
        guibutton $tjt $tjc
    ]
]


resetlight = [
    lightcolour = 0
    lightbright = 1
    lightset 255 255 255
    lightradius = 128
]

lightset = [
    lightr = $arg1
    lightg = $arg2
    lightb = $arg3
    if (= $lightbright 0) lightscale
]

lightscale = [
    if (= $lightbright 0) [
        lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
    ] [
        lightr = (min 255 (* $lightr 2)); lightg = (min 255 (* $lightg 2)); lightb = (min 255 (* $lightb 2))
    ]
]

lightcmd = [
    result (concat newent light $lightradius $lightr $lightg $lightb)
]

newgui newlight [
    guibutton "t:gui.light.create" (lightcmd)
    guibar
    guitext "t:gui.light.color"
    guicolor (+ (* (+ (* $lightr 256) $lightg) 256) $lightb)
    guislider lightr 0 255
    guislider lightg 0 255
    guislider lightb 0 255
    guilist [
        guicheckbox "t:gui.light.bright" lightbright 1 0 [lightscale]
        guibar
        guiradio "t:gui.light.white" lightcolour 0 [lightset 255 255 255]
        guiradio "t:gui.light.blue" lightcolour 1 [lightset 192 192 255]
        guiradio "t:gui.light.red" lightcolour 2 [lightset 255 192 192]
        guiradio "t:gui.light.green" lightcolour 3 [lightset 192 255 192]
        guiradio "t:gui.light.yellow" lightcolour 4 [lightset 255 255 192]
        guiradio "t:gui.light.purple" lightcolour 5 [lightset 255 192 255]
        guiradio "t:gui.light.turq" lightcolour 6 [lightset 192 255 255]
    ]
    guitext "radius:"
    guislider lightradius 0 512
] "t:gui.light"

newgui newteleport [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
] "t:gui.teleport"

newgui mapmodels [
    loop i (div (+ (nummapmodels) (- 17 1)) 17) [
        if (> $i 0) [
            guitab (+ $i 1)
        ]
        guilist [
            guilist [
                loop j (min (- (nummapmodels) (* $i 17)) 17) [
                    mmidx = (+ (* $i 17) $j)
                    mmname = (mapmodelname $mmidx)
                    if (= (strstr $mmname "mapmodels") 0) [
                        mmname = (substr $mmname 10)
                    ]
                    guibutton $mmname [newent mapmodel @mmidx]
                ]
            ]
            guispring
            guibar
            guimodelpreview (mapmodelname (at $guirolloveraction 2)) "mapmodel" (checkrolloveraction "mapmodel ") 4 1
        ]
    ]
]

newgui postfx [
    guibutton "(effect OFF)"            "clearpostfx"
	guibutton "bloom (very subtle: 5%)" "bloom 0.05"
    guibutton "bloom (subtle: 30%)"     "bloom 0.3"
    guibutton "bloom (bright: 55%)"     "bloom 0.55"
    guibutton "bloom (intense: 80%)"    "bloom 0.8"
    guibutton "rotoscope" "rotoscope 1"
    guibutton "rotoscope + blur3" "rotoscope 1 1"
    guibutton "rotoscope + blur5" "rotoscope 1 2"
	guibutton "toast" "setpostfx toast"
    guibutton "sobel"  "setpostfx sobel"
    guibutton "invert" "setpostfx invert"
    guibutton "gbr"    "setpostfx gbr"
    guibutton "bw"     "setpostfx bw"
    guibutton "blur3"  "setpostfx hblur3; addpostfx vblur3"
    guibutton "blur5"  "setpostfx hblur5; addpostfx vblur5"
	guibutton "experimental" "setpostfx test3"
]

postfx = [
	showgui postfx
]

macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" (if (&& (= $scr_w %1) (= $scr_h %2)) [result "radio_on"] [result "radio_off"])
]

rifler = 64
rifleg = 64
rifleb = 64

rifletrailcmd = [
    result (concat rifletrail $rifler $rifleg $rifleb)
]

newgui rifletrail [
    guibutton "t:gui.rifletrail.set" (rifletrailcmd)
    guibar
	guilist [
		guitext "t:gui.rifletrail.old"
		guicolor ($rifletrail)
    ]
	guilist [
		guitext "t:gui.rifletrail.new"
		guicolor (+ (* (+ (* $rifler 256) $rifleg) 256) $rifleb)
    ]
	guislider rifler 0 255
    guislider rifleg 0 255
    guislider rifleb 0 255
] "t:gui.rifletrail"

bloodr = 159
bloodg = 0
bloodb = 0

bloodcmd = [
    result (concat bloodcolor $bloodr $bloodg $bloodb)
]

newgui bloodcolor [
    guibutton "t:gui.bloodcolor.set" (bloodcmd)
    guibar
	guilist [
		guitext "t:gui.bloodcolor.old"
		guicolor ($bloodcolor)
    ]
	guilist [
		guitext "t:gui.bloodcolor.new"
		guicolor (+ (* (+ (* $bloodr 256) $bloodg) 256) $bloodb)
    ]
	guislider bloodr 0 255
    guislider bloodg 0 255
    guislider bloodb 0 255
] "t:gui.bloodcolor"

showalbumart = [
    guibar
    guiimage (concatword "packages/music/" (if (> $numargs 0) [result $arg1] [at $guirollovername 0]) "/front.png") (checkrolloveraction "soundtrack ") 4 1 "data/cardboard_missing.png"
]

newgui soundtrack [
	guistayopen [
		guitext (concat "Soundtrack:" $soundtrack)
		guitext (concatword "^f1" $_soundtrack_title)
		guitext (concatword "By " $_soundtrack_author)
		if (!=s $_soundtrack_url "") [
			guitext (concatword "^f0" $_soundtrack_url)
		]
		guibar
		guilist [
			guilist [ 
				guistrut 15 1
				looplist curst $soundtracks1 [
					guibutton $curst (concat soundtrack $curst)
				]
			]
			guilist [ 
				guistrut 15 1
				looplist curst $soundtracks2 [
					guibutton $curst (concat soundtrack $curst)
				]
			]
			showalbumart
		]
		//looplist curst $soundtracks [
		//	guibutton $curst (concat soundtrack $curst)
		//]
	]
] 0

showsoundtracks = [
    soundtracks = ""
    loopdir curst "packages/music" [
       soundtracks = (concat $soundtracks $curst)
    ]
    soundtracks = (sortlist $soundtracks x y [<=s $x $y])
	soundtracks1 = ""
	soundtracks2 = ""

	looplist2 st1 st2 $soundtracks [
		soundtracks1 = (concat $soundtracks1 $st1)
		soundtracks2 = (concat $soundtracks2 $st2)
	]
    showgui soundtrack
]

genlangitems = [
    looplist curlang $arg1 [
        guibutton $curlang (concat language $curlang) (concatword "lang/" $curlang)
    ]
]

newgui lang [
    langavail = ""
	loopfiles curlang "packages/lang" "cfg" [
		langavail = (concat $langavail (escape $curlang))
	]
	langavail = (sortlist $langavail x y [<=s $x $y])

    genlangitems $langavail
] "Select language"

newgui options [
    guicheckbox "2D menus" gui2d
    guicheckbox "Show scoreboard at death" deathscore
    guicheckbox "Blood"             blood
    guilist [
        guicheckbox "Ragdoll deaths"    ragdoll
        if $ragdoll [
            guibar
            guicheckbox "Keep after respawn" ragdollmillis 10000
        ]
        guibar
        guicheckbox "Hide dead players" hidedead
    ]
    guitext "Ragdoll velocity multiplier"
    guislider deadpush
    guibar
    guilist [
        guitext "Language: "
        guibutton $language [showgui lang] (concatword "lang/" $language)
    ]
    guibar
    guibutton "Restore defaults (clears ALL settings)" "exec restore.cfg"

	guitab    "t:gui.options.multiplayer"
	guitext   "There's nothing here right now..."

    guitab    "t:gui.options.display"
    guibutton "Screen resolution" "showgui resolution"
    guibutton "Graphics settings" "showgui graphics"
    guibar
    guicheckbox "Vertical sync"            vsync 1 0
    guilist [
        guiradio "Windowed" fullscreen 0
        guibar
        guiradio "Borderless windowed" fullscreen 2
        guibar
        guiradio "Fullscreen" fullscreen 1
	]
    guibar
	guitext "Texture Quality Reduction (default: 0)"
    guislider texreduce
    guitext "Gamma (default: 100)"
    guislider gamma
    guitext "FSAA (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "Z-Buffer Depth (default: 0)"
    guilistslider depthbits "0 16 24 32"
    guitext "Anisotropic Filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"
	guilist [
    	guicheckbox "Bilinear Filtering" bilinear
		guibar
		guicheckbox "Trilinear Filtering (mipmaps)" trilinear
	]

    guitab "t:gui.options.audio"
    guitext "Sound volume"
    guislider soundvol
    guitext "Music volume"
    guislider musicvol
    guitext "Sound channels"
    guislider soundchans
    guitext "Sound frequency (sample rate)"
    guilistslider soundfreq "11025 22050 44100"
    guitext "Sound buffer length"
    guislider soundbufferlen
    //guicheckbox "Mumble positional audio" mumble
	guibar
	guibutton (concat "Soundtrack:" $soundtrack) [showsoundtracks]

    guitab  "t:gui.options.mouse"
    guicheckbox "Invert mouse"      invmouse

    guilist [
        guitext "Mouse sensitivity: "
        newsens = $sensitivity
        guifield newsens 8 [sensitivity $newsens]
    ]

    guilist [
        guitext "Mouse UI sensitivity: "
        newguisens = $guisens
        guifield newguisens 8 [guisens $newguisens]
    ]
    guibar
    guilist [
        guibutton "Crosshair: " [showgui crosshair]
        guiimage (getcrosshair) [showgui crosshair] 0.5
    ]
    guitext "Crosshair size: "
    guislider crosshairsize
    guicheckbox "Hit sound" hitsound

    guitab "t:gui.options.input"
    guitext "(Basic keybinds, for anything more use the 'bind' command)"
    guitext "Select action to bind and press desired keys (ESC when done):"

    guilistsplit n 2 $binddesc [
        guilist [
            guitext (tabify (concatword $n ": ") 4)
            gay = (at $bindactions $i)
            [newbinds@i] = (searchbinds $gay)
            guikeyfield [newbinds@i] -9 [
                oldbinds = (searchbinds [@@gay])
                looplist j $oldbinds [bind $j ""]
                looplist j $[newbinds@@i] [bind $j [@@@gay]]
            ]
        ]
    ] [guibar]

    guitab "t:gui.options.console"
    guicheckbox "Chat console" miniconfilter 0x300 0
    guitext "Console size (lines)"
    guislider consize
    guitext "Console filter:"
    guilist [
        guilist [
            guibitfield "Important information" confilter 0x01
            guibitfield "Warnings" confilter 0x02
            guibitfield "Errors" confilter 0x04
            guibitfield "Init messages" confilter 0x10
            guibitfield "Script messages" confilter 0x20
	    ]
        guibar
        guilist [
            guibitfield "Chat" confilter 0x100
            guibitfield "Team chat" confilter 0x200
            guibitfield "Game events" confilter 0x400
            guibitfield "Self frags" confilter 0x800
            guibitfield "Other player frags" confilter 0x1000
        ]
        guibar
        guilist [
            guibitfield "Team kills" confilter 0x2000
        ]
    ]
    guitext "Full console size (percent of screen)"
    guislider fullconsize
    guitext "Full console filter:"
    guilist [
        guilist [
            guibitfield "Important information" fullconfilter 0x01
            guibitfield "Warnings" fullconfilter 0x02
            guibitfield "Errors" fullconfilter 0x04
            guibitfield "Init messages" fullconfilter 0x10
            guibitfield "Script messages" fullconfilter 0x20
        ]
        guibar
        guilist [
            guibitfield "Chat" fullconfilter 0x100
            guibitfield "Team chat" fullconfilter 0x200
            guibitfield "Game events" fullconfilter 0x400
            guibitfield "Self frags" fullconfilter 0x800
            guibitfield "Other player frags" fullconfilter 0x1000
        ]
        guibar
        guilist [
            guibitfield "Team kills" fullconfilter 0x2000
        ]
    ]

	guitab "HUD"
	guicheckbox "Show FPS" showfps
    guilist [
        guiradio "30 FPS" maxfps 30
        guibar
        guiradio "60 FPS" maxfps 60
        guibar
        guiradio "200 FPS" maxfps 200
        guibar
        guiradio "Unlimited FPS" maxfps 1000
    ]
    guilist [
        guicheckbox "Wall clock" wallclock
        if $wallclock [
            guibar
            guicheckbox "24 hour" wallclock24
            guibar
            guicheckbox "Seconds" wallclocksecs
        ]
    ]
    guilist [
        guicheckbox "Damage screen"     damagescreen
        guibar
        guicheckbox "Damage compass"    damagecompass
    ]
	guilist [
    	guicheckbox "HUD guns"       hudgun
		if $hudgun [
			guibar
			guicheckbox "Sway"		hudgunsway
            guibar
            guicheckbox "Muzzle flash" muzzleflash
            guibar
            guicheckbox "Muzzle light" muzzlelight
		]
	]
    guilist [
        guicheckbox "Crosshair color effects" crosshairfx
        if $crosshairfx [
            guibar
            guicheckbox "Health colors" healthcrosshair
            guibar
            guicheckbox "Show teammates" teamcrosshair
            guibar
            guicheckbox "Show hits" hitcrosshair 425
        ]
    ]
    guilist [
        guicheckbox "Healthbar" healthbar
        guibar
        guicheckbox "Health colors" healthcolors
    ]
    guilist [
        guicheckbox "Weapons bar" ammohud
        if $ammohud [
            guibar
            guicheckbox "Spacing" ammohudspacing
            guibar
            guicheckbox "Hide melee" ammohudhidemelee
        ]
    ]
    guibar
    guilist [
        guicheckbox "Game clock" gameclock
        if $gameclock [
            guibar
            guibutton "Configure" [showgui gameclock]
        ]
    ]
    guilist [
        guicheckbox "HUD score" hudscore
        if $hudscore [
            guibar
            guibutton "Configure" [showgui hudscore]
        ]
    ]

    guitab "t:gui.options.autoexec"
    guitext "autoexec.cfg"
    showfileeditor "autoexec.cfg" -60 15
] "t:gui.options.game"

newgui gameclock [
    guilist [
        guitext "Alignment: "
        guiradio "Left" gameclockalign -1
        guibar
        guiradio "Center"  gameclockalign  0
        guibar
        guiradio "Right"   gameclockalign  1
    ]

    guitext "Alpha: "
    guislider gameclockalpha

    // gameclockcolour    0 - 0xFFFFFF
    // gameclocklowcolour 0 - 0xFFFFFF

    guilist [
        guitext "Scale: "
        guifield gameclockscale 5
        guistayopen [ guibutton "- " [gameclockscale (-f $gameclockscale 0.01)] ]
        guistayopen [ guibutton "+"  [gameclockscale (+f $gameclockscale 0.01)] ]
    ]

    guilist [
        guitext "X: "
        guifield gameclockx 5
        guistayopen [ guibutton "- " [gameclockx (-f $gameclockx 0.01)] ]
        guistayopen [ guibutton "+"  [gameclockx (+f $gameclockx 0.01)] ]
    ]
    guilist [
        guitext "Y: "
        guifield gameclocky 5
        guistayopen [ guibutton "- " [gameclocky (-f $gameclocky 0.01)] ]
        guistayopen [ guibutton "+"  [gameclocky (+f $gameclocky 0.01)] ]
    ]
] "Game clock"

newgui hudscore [
    guilist [
        guitext "Alignment: "
        guiradio "Left" hudscorealign -1
        guibar
        guiradio "Center"  hudscorealign  0
        guibar
        guiradio "Right"   hudscorealign  1
    ]

    guitext "HUD score alpha: "
    guislider hudscorealpha

    // hudscoreenemycolour  0 - 0xFFFFFF
    // hudscoreplayercolour 0 - 0xFFFFFF

    guitext "Spacing: "
    guislider hudscoresep

    guilist [
        guitext "Scale: "
        guifield hudscorescale 5
        guistayopen [ guibutton "- " [hudscorescale (-f $hudscorescale 0.01)] ]
        guistayopen [ guibutton "+"  [hudscorescale (+f $hudscorescale 0.01)] ]
    ]

    guilist [
        guitext "X: "
        guifield hudscorex 5
        guistayopen [ guibutton "- " [hudscorex (-f $hudscorex 0.01)] ]
        guistayopen [ guibutton "+"  [hudscorex (+f $hudscorex 0.01)] ]
    ]
    guilist [
        guitext "Y: "
        guifield hudscorey 5
        guistayopen [ guibutton "- " [hudscorey (-f $hudscorey 0.01)] ]
        guistayopen [ guibutton "+"  [hudscorey (+f $hudscorey 0.01)] ]
    ]
] "HUD score"

newgui resolution [
    guitext "Field of view (default: 100)"
    guislider fov
    guistayopen [
      guilist [
        guilist [
            guitext "4:3"
            @@@@(resbutton 320 240)
            @@@@(resbutton 640 480)
            @@@@(resbutton 800 600)
            @@@@(resbutton 1024 768)
            @@@@(resbutton 1152 864)
            @@@@(resbutton 1280 960)
            @@@@(resbutton 1400 1050)
            @@@@(resbutton 1600 1200)
            @@@@(resbutton 1792 1344)
            @@@@(resbutton 1856 1392)
            @@@@(resbutton 1920 1440)
            @@@@(resbutton 2048 1536)
            @@@@(resbutton 2800 2100)
            @@@@(resbutton 3200 2400)
        ]
        guibar
        guilist [
            guitext "16:10"
            @@@@(resbutton 320 200)
            @@@@(resbutton 640 400)
            @@@@(resbutton 1024 640)
            @@@@(resbutton 1280 800)
            @@@@(resbutton 1440 900)
            @@@@(resbutton 1600 1000)
            @@@@(resbutton 1680 1050)
            @@@@(resbutton 1920 1200)
            @@@@(resbutton 2048 1280)
            @@@@(resbutton 2560 1600)
            @@@@(resbutton 2880 1800)
            @@@@(resbutton 3200 2000)
            @@@@(resbutton 3840 2400)
        ]
        guibar
        guilist [
            guitext "16:9"
            @@@@(resbutton 1024 600)
            @@@@(resbutton 1280 720)
            @@@@(resbutton 1366 768)
            @@@@(resbutton 1600 900)
            @@@@(resbutton 1920 1080)
            @@@@(resbutton 2048 1152)
            @@@@(resbutton 2560 1440)
            @@@@(resbutton 2880 1620)
            @@@@(resbutton 3200 1800)
            @@@@(resbutton 3840 2160)
        ]
        guibar
        guilist [
            guitext "5:4"
            @@@@(resbutton 600 480)
            @@@@(resbutton 1280 1024)
            @@@@(resbutton 1600 1280)
            @@@@(resbutton 2560 2048)
        ]
        guibar
        guilist [
            guitext "5:3"
            @@@@(resbutton 800 480)
            @@@@(resbutton 1280 768)

            guibar
            guitext "Custom"
            guilist [
                customw = $scr_w
                customh = $scr_h
                guifield customw 4 [scr_w $customw]
                guifield customh 4 [scr_h $customh]
            ]
        ]
      ]
    ]
] "t:gui.options.resolution"

newgui graphics [
    guitext "Performance key: ^f0fast^f~, ^f2moderate^f~, ^f3slow and pretty^f~"
    guibar
	guilist [
       	guitext "Shaders		"
		guibar
        guiradio "^f0OFF" shaderdetail 0
        guibar
        guiradio "^f0Low detail" shaderdetail 1
        guibar
        guiradio "^f0Mid detail" shaderdetail 2
        guibar
        guiradio "^f2High detail" shaderdetail 3
	]
    guilist [
        guitext "Water		"
        guibar
        guicheckbox "^f2Refraction"  waterrefract
        guibar
        guicheckbox "^f3Reflection"  waterreflect
        guibar
        guicheckbox "^f0Caustics"    caustics
        guibar
        guicheckbox "^f2Animation" vertwater
    ]
	guilist [
		guitext "Waterfalls		"
		guibar
		guicheckbox "^f2Refraction" waterfallrefract
		guibar
		guicheckbox "^f0Reflection" waterfallenv
	]
	guilist [
       	guicheckbox "^f3Soft shadows	"  shadowmap
		if $shadowmap [
            guibar
      	    guiradio "^f2Medium quality" shadowmapsize 9 [blurshadowmap 1]
   	        guibar
            guiradio "^f3High quality" shadowmapsize 10 [blurshadowmap 2]
		] [
			guibar
			guicheckbox "^f0Blob shadows" blobs
		]
   	]
   	if $glare [
       	glarepreset = 0
       	if (= $glarescale 1) [
           	if (= $blurglare 4) [glarepreset = 1]
           	if (= $blurglare 7) [glarepreset = 3]
       	]
       	if (= $glarescale 2) [
           	if (= $blurglare 3) [glarepreset = 2]
           	if (= $blurglare 7) [glarepreset = 4]
       	]
       	guilist [
           	guicheckbox "^f3Glare			"       glare
           	guibar
           	guiradio "^f2Subtle" glarepreset 1 [blurglare 4; glarescale 1]
           	guibar
           	guiradio "^f2Glowy" glarepreset 2 [blurglare 3; glarescale 2]
           	guibar
           	guiradio "^f3Soft" glarepreset 3 [blurglare 7; glarescale 1]
           	guibar
           	guiradio "^f3Intense" glarepreset 4 [blurglare 7; glarescale 2]
       	]
   	] [
       	guicheckbox "^f3Glare"         glare
   	]
    guilist [
        guicheckbox "^f3Motion blur	" motionblur
        if $motionblur [
            guibar
            guiradio "^f3Subtle" motionblurscale 0.5
            guibar
            guiradio "^f3Moderate" motionblurscale 0.65
            guibar
            guiradio "^f3Intense" motionblurscale 0.8
        ]
    ]
    guilist [
        guicheckbox "^f3Grass			" grass
		if $grass [
			guibar
			guiradio "^f2Quick fade" grassdist 128
			guibar
			guiradio "^f2Moderate fade" grassdist 256
			guibar
			guiradio "^f3Slow fade" grassdist 512
		]
	]
    guilist [
        guicheckbox "^f0Dynamic lights	"    maxdynlights 3 0
        if $maxdynlights [
            guibar
            guiradio "^f0Medium quality" maxdynlights 3
            guibar
            guiradio "^f2High quality" maxdynlights 5
        ]
    ]
    guilist [
        guicheckbox "^f0Soft particles	" depthfx
        if $depthfx [
            guibar
            guiradio "^f0Low quality" depthfxsize 7 [blurdepthfx 1]
            guibar
            guiradio "^f2Medium quality" depthfxsize 10 [blurdepthfx 0]
            guibar
            guiradio "^f3High quality" depthfxsize 12 [blurdepthfx 0]
        ]
	]
    guicheckbox "^f0Glass reflection"  glassenv
    guilist [
        guicheckbox "^f0Decals		" decals
        if $decals [
            guibar
            guiradio "^f0Quick fade" decalfade 10000 [maxdecaltris 1024]
            guibar
            guiradio "^f2Slow fade" decalfade 60000 [maxdecaltris 4096]
        ]
    ]
    guicheckbox "^f0Fix t-joints (world sparklies)" filltjoints
    guilist [
        guitext "Textures		"
        guibar
        guiradio "^f0Low quality" maxtexsize 256
        guibar
        guiradio "^f0Medium quality" maxtexsize 512
        guibar
        guiradio "^f2High quality" maxtexsize 0
    ]
    guilist [
        guitext "Models		"
        guibar
        guicheckbox "^f0Lighting"    lightmodels
        guibar
        guicheckbox "^f0Reflection"  envmapmodels
        guibar
        guicheckbox "^f0Glow"        glowmodels
        guibar
        guicheckbox "^f2Bumpmap" bumpmodels
    ]
] "t:gui.options.graphics"

bindactions = [forward backward left right jump attack taunt togglezoom holdzoom saycommand sayteamcommand showscores toggleconsole screenshot edittoggle togglespacepack "setweapon FI" "setweapon MG" "setweapon SG" "setweapon RI" "setweapon CG" "setweapon RL" "setweapon GL" "weapon" dropflag addbot delbot]
binddesc = ["Forward" "Backward" "Left" "Right" "Jump" "Attack" "Taunt" "Toggle zoom" "Hold zoom" "Chat" "Team chat" "Show scores" "Command" "Screenshot" "Edit mode" "Toggle spacepack" "Melee" "SMG" "Shotgun" "Sniper" "Chaingun" "Rocket" "Grenade" "Last weapon" "Drop flag" "Add bot" "Remove bot"]

entupdate = [ entset $entguitype $entguiattr0 $entguiattr1 $entguiattr2 $entguiattr3 $entguiattr4 ]

initentgui = [
  entguitype = (enttype)
  @(loopconcat i 5 [result [
    entguiattr@i = (entattr @i)
  ]])
]

genentattributes = [
    entattributes = (loopconcat i (listlen $arg2) [
        entattribname = (at $arg2 $i)
        entattriblimits = (at $arg3 (* 2 $i))
        entattriblimits2 = (at $arg3 (+ 1 (* 2 $i)))
        if (=s (at $entattriblimits 0) $entattriblimits) [
            result [
                guitext @entattribname
                guislider entguiattr@i @entattriblimits @entattriblimits2 entupdate
            ]
        ] [
            result [
                guitext @entattribname
                guinameslider entguiattr@i [@@entattriblimits] [@@entattriblimits2] entupdate
            ]
        ]
    ])
]

guilistsplit = [
  guilist [
    i = 0
    l = (listlen $arg3)
    z = (div (+ $l (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $l)
        while [< $i $t] [
          $arg1 = (at $arg3 $i)
          arg4
          i = (+ $i 1)
        ]
      ]
      if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [arg5]
    ]
  ]
]

quickeditmenu = [
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton "Quick calclight"   "calclight -1"
  guibutton "Optimize map"      "remip"
  guibutton "New entity"        "newent shells"
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
] "t:gui.quickedit"

matmenu = [
    guibutton "air" "editmat air"
    guibutton "water" "editmat water"
    guibutton "lava" "editmat lava"
    guibutton "clip" "editmat clip"
    guibutton "glass" "editmat glass"
    guibutton "noclip" "editmat noclip"
    guibutton "gameclip" "editmat gameclip"
    guibutton "death" "editmat death"
    guibutton "alpha" "editmat alpha"
	guibutton "spaceclip" "editmat spaceclip"
    guicheckbox "t:gui.materials.showmat" showmat
]

brushmenu = [
  guilist [ // diamonds
  guiimage "packages/icons/brush_1c.png" brush_0
  guiimage "packages/icons/brush_21c.png" brush_1
  guiimage "packages/icons/brush_421c.png" brush_2
  ]
  guilist [ // squares
  guiimage "packages/icons/brush_3s.png" brush_3
  guiimage "packages/icons/brush_5s.png" brush_4
  guiimage "packages/icons/brush_7s.png" brush_5
  ]
  guititle smooth
  guilist [ // smooth
  guiimage "packages/icons/brush_3s.png" brush_6
  guiimage "packages/icons/brush_5s.png" brush_7
  guiimage "packages/icons/brush_7s.png" brush_8
  ]
]

newentgui = [
  genentattributes $arg1 $arg2 $arg3
  newgui $arg1 [
    guitext $entguitype
    guibar
    @entattributes
    guitab type
    guilistsplit n 2 $enttypelist [
        guibutton $n [ entset @n ]
    ]
    guitab misc
    @quickeditmenu
  ] "" [initentgui]
]

looplist i $enttypelist [
  newentgui $i "" ""
]

newgui materials [
  @matmenu
  guitab misc
  @quickeditmenu
]

newgui brushes [
  @brushmenu
  guitab misc
  @quickeditmenu
]

newgui quickedit [
  @quickeditmenu
  guitab materials
  @matmenu
] 

newentgui light "Radius Red Green Blue" "0 400 0 255 0 255 0 255"
newentgui spotlight "Radius" "0 200"
newentgui playerstart "Direction Team" "0 360 0 2"
newentgui teleport "Tag" "0 20"
newentgui teledest "Direction Tag" "0 360 0 20"
newentgui mapmodel "Direction Model" "0 360 0 100"
newentgui envmap "Radius" "0 400"
newentgui jumppad "Z Y X" "0 200 0 200 0 200"
newentgui sound "Type Radius Size" "0 20 0 500 0 500"
newentgui particles "Type" "0 3"
newentgui flag "Direction Team" "0 360 0 2"

contexteditgui = [
    if $hmapedit [showgui brushes] [
        if (enthavesel) [
            showgui (enttype)
        ] [showgui (? (havesel) materials quickedit)]
    ]
]

showentgui = [ contexteditgui ] // legacy bind

newgui seltexinfo [
    guitext (concatword "Slot: " (getseltex))
    guilist [ loop x 5 [
        texname = (gettexname (getseltex) $x)
        texname = (substr $texname (+ (strstr $texname ">") 1)) // chop off any leading commands
        guiimage (concatword "packages/" $texname) "" 1 1
    ]]
    loop x 5 [ tex = (gettexname (getseltex) $x); if (strcmp $tex "") [] [guitext $tex]]
]
