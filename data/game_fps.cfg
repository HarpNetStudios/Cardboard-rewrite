// fps specific cfg stuff goes here
togglezoom = [ zoom (if (= $zoom 1) -1 1) ]
holdzoom = [ zoom 1; onrelease [zoom -1] ]
sayteamcommand = [inputcommand (if (>= $numargs 1) [result $arg1] [result ""]) [sayteam $commandbuf] "[team]"]

mapcomplete = [ complete $arg1 packages/base cmr ]
mapcomplete map

modenames = "ffa coop tdm insta instateam tac tacteam capture regencapture ctf instactf protect instaprotect hold instahold collect instacollect parkour instatac instatacteam grenade gungame lms ectf r1ctf race"

loop i (listlen $modenames) [
    mname = (at $modenames $i)
    alias $mname [ if (mode @i) [if (> $numargs 0) [map $arg1] [map]] ]
    mapcomplete $mname
]

demo = [ stopdemo; if (mode -1) [if (> $numargs 0) [map $arg1] [map]] ]
varcomplete demo demodir dmo

playermodelnum = 1

playermodeldir0 = "prototype"
playermodelicon0 = "prototype"
playermodelname0 = "Prototype 9X"
playermodelstory0 = [Prototype 9X was created as a placeholder instead of humans in weapons testing for the NUHA in 2043. It remains a large part of weapons testing to this day.]

playermodeldir = [getalias (concatword "playermodeldir" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelicon = [getalias (concatword "playermodelicon" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelname = [getalias (concatword "playermodelname" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelstory = [getalias (concatword "playermodelstory" (if (> $numargs 0) [result $arg1] [result $playermodel]))]

exec (concatword "packages/music/" $soundtrack "/soundtrack.cfg")
playsong = [ music (concatword "music/" $soundtrack "/" (+ 1 (rnd $musicnum)) ".ogg") playsong ]


